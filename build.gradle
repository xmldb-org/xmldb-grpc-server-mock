/*
 * Licensed under the Apache License, Version 2.0 (the "License") you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0 Unless required by applicable law or agreed to in
 * writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */

plugins {
  id 'eclipse'
  id 'idea'
  id 'java-library'
  id 'java-test-fixtures'
  id 'io.quarkus' version '3.29.3'
  id 'com.diffplug.spotless' version '8.1.0'
  id 'org.kordamp.gradle.java-project' version '0.54.0'
}

apply {
  plugin 'org.kordamp.gradle.checkstyle'
  plugin 'org.kordamp.gradle.errorprone'
  plugin 'org.kordamp.gradle.sonar'
}

if (!project.hasProperty('sonatypeUsername')) ext.sonatypeUsername = '**undefined**'
if (!project.hasProperty('sonatypePassword')) ext.sonatypePassword = '**undefined**'
if (!project.hasProperty('sonarToken')) ext.sonarToken = '**undefined**'

group = 'net.sf.xmldb-org'

defaultTasks 'build'

java {
  sourceCompatibility = JavaVersion.VERSION_21
}

sourceSets {
  main {
    java {
      srcDirs += 'build/classes/java/quarkus-generated-sources/grpc'
    }
  }
}

repositories {
  mavenCentral()
  maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
}

dependencies {
  implementation enforcedPlatform('io.quarkus.platform:quarkus-bom:3.29.3')
  implementation 'io.quarkus:quarkus-grpc'
  implementation 'io.quarkus:quarkus-arc'
  implementation 'net.sf.xmldb-org:xmldb-api:3.0.0-protobuf-SNAPSHOT'

  runtimeOnly 'ch.qos.logback:logback-classic:1.5.21'

  testImplementation 'org.assertj:assertj-core:3.27.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.1'
  testImplementation 'org.mockito:mockito-core:5.20.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'

  testRuntimeOnly 'org.mockito:mockito-core:5.20.0'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.14.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
}

licenseMain {
  dependsOn 'compileQuarkusGeneratedSourcesJava'
}

jar {
  manifest.attributes 'Implementation-Title': 'XML:DB API specification',
                      'Specification-Title': 'XML:DB API specification',
                      'Automatic-Module-Name': "org.xmldb.remote"
}

tasks.withType(JavaCompile).configureEach {
  dependsOn 'spotlessJavaApply', 'quarkusGenerateCode'
}

tasks.withType(Jar).configureEach {
  dependsOn 'compileQuarkusGeneratedSourcesJava'
}

tasks.withType(com.diffplug.gradle.spotless.SpotlessTask).configureEach {
  dependsOn 'quarkusGenerateCode', 'quarkusGenerateCodeDev'
}

spotless {
  java {
    eclipse().configFile('gradle/xmldb-remote-style.xml')
    importOrderFile('gradle/xmldb-remote.importorder')
    replace('generated', '@javax.annotation.Generated', '@jakarta.annotation.Generated')
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'skipped'
  }
}

config {
  release = !project.version.toString().endsWith('-SNAPSHOT')
  info {
    vendor = 'reinhapa'
    name = 'XML:DB Proto buffers remote client'
    description = 'XML:DB Initiative for XML Databases'
    inceptionYear = '2023'
    links {
      website = 'https://github.com/xmldb-org/xmldb-remote'
      issueTracker = 'https://github.com/xmldb-org/xmldb-remote/issues'
      scm = 'https://github.com/xmldb-org/xmldb-remote.git'
    }
    scm {
      connection = 'scm:git://github.com/xmldb-org/xmldb-remote.git'
      developerConnection = 'scm:git://github.com/xmldb-org/xmldb-remote.git'
      url = 'https://github.com/xmldb-org/xmldb-remote'
    }
    organization {
      name = 'XML:DB Initiative for XML Databases'
      url = 'https://github.com/xmldb-org'
    }
    people {
      person {
        id = 'xmldb-remote'
        name = 'XML:DB Initiative'
        roles = [ 'owner' ]
      }
      person {
        id = 'reinhapa'
        name = 'Patrick Reinhart'
        email = 'patrick@reini.net'
        roles = [ 'developer' ]
      }
    }
    repositories {
      repository {
        name = 'sonatypeRelease'
        url  = 'https://ossrh-staging-api.central.sonatype.com/service/local/'
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
      }
      repository {
        name = 'sonatypeSnapshot'
        url  = 'https://central.sonatype.com/repository/maven-snapshots/'
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
      }
    }
  }
  quality {
    errorprone {
      errorProneVersion = '2.28.0'
      excludedPaths = '.*/quarkus-generated-sources/grpc/.*'
    }
    sonar {
      hostUrl = 'https://sonarcloud.io'
      login = sonarToken
      organization = 'xmldb-org'
      projectKey = 'xmldb-org_xmldb-remote'
    }
  }
  licensing {
    excludes = [ 'org/xmldb/api/grpc/**' ]
    licenses {
      license {
        name = 'The XML:DB Initiative Software License'
        url = 'https://github.com/xmldb-org/xmldb-remote/raw/master/LICENSE'
      }
    }
  }
  docs {
    javadoc {
      autoLinks.enabled = false
      options {
        docTitle = "XML:DB ${project.version} grpc remote client"
        header = "XML:DB ${project.version} grpc remote client"
        windowTitle = "XML:DB ${project.version} grpc remote client"
      }
    }
  }
  artifacts {
    minpom.enabled = false
  }
  publishing {
    signing.enabled = release
    releasesRepository  = 'sonatypeRelease'
    snapshotsRepository = 'sonatypeSnapshot'
  }
}

normalization {
  runtimeClasspath {
    ignore '/META-INF/MANIFEST.MF'
  }
}
