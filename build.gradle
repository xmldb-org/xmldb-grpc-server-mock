/*
 * Licensed under the Apache License, Version 2.0 (the "License") you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0 Unless required by applicable law or agreed to in
 * writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */

plugins {
  id 'eclipse'
  id 'idea'
  id 'java-library'
  id 'io.quarkus' version '3.31.3'
  id 'com.diffplug.spotless' version '8.2.1'
  id 'net.ltgt.errorprone' version '5.0.0'
}

group = 'net.sf.xmldb-org'

java {
  sourceCompatibility = JavaVersion.VERSION_21
  withJavadocJar()
  withSourcesJar()
}

sourceSets {
  main {
    java {
      srcDirs += 'build/classes/java/quarkus-generated-sources/grpc'
    }
  }
}

repositories {
  mavenCentral()
  maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
}

configurations.configureEach {
  resolutionStrategy {
    cacheChangingModulesFor(0, 'seconds')
    cacheDynamicVersionsFor(0, 'seconds')
  }
}

dependencies {
  errorprone 'com.google.errorprone:error_prone_core:2.47.0'

  implementation enforcedPlatform('io.quarkus.platform:quarkus-bom:3.31.2')
  implementation 'io.quarkus:quarkus-grpc'
  implementation 'io.quarkus:quarkus-arc'
  implementation 'net.sf.xmldb-org:xmldb-api:3.0.0-SNAPSHOT'
  implementation 'net.sf.xmldb-org:xmldb-grpc-client:0.1.0-SNAPSHOT:proto'
  implementation 'net.sf.xmldb-org:xmldb-mockdb:0.1.0-SNAPSHOT'

  runtimeOnly 'ch.qos.logback:logback-classic:1.5.29'

  testImplementation 'org.assertj:assertj-core:3.27.7'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
  testImplementation 'org.mockito:mockito-core:5.21.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'

  testRuntimeOnly 'org.mockito:mockito-core:5.21.0'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.14.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
}

compileJava {
  options.errorprone {
      excludedPaths = '.*/build/classes/java/quarkus-generated-sources/.*'
      allErrorsAsWarnings = false
      disableWarningsInGeneratedCode = true
  }
}

javadoc {
  excludes = ['org/xmldb/api/grpc/**']
}

jar {
  manifest.attributes 'Implementation-Title': 'XML:DB gRPC API quarkus backend implementation',
                      'Automatic-Module-Name': "org.xmldb.remote"
}

tasks.withType(JavaCompile).configureEach {
  dependsOn 'spotlessJavaApply', 'quarkusGenerateCode'
  options.compilerArgs += ['-Xlint:unchecked', '-XDaddTypeAnnotationsToSymbol=true']
}

tasks.withType(Jar).configureEach {
  dependsOn 'compileQuarkusGeneratedSourcesJava'
}

tasks.withType(com.diffplug.gradle.spotless.SpotlessTask).configureEach {
  dependsOn 'quarkusGenerateCode', 'quarkusGenerateCodeDev'
}

spotless {
  java {
    eclipse().configFile('gradle/xmldb-remote-style.xml')
    importOrderFile('gradle/xmldb-remote.importorder')
    replace('generated', '@javax.annotation.Generated', '@jakarta.annotation.Generated')
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'skipped'
  }
}
